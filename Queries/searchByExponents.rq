# find data associated with a set of values of the exponents
# fill in the values for exponents to do the search

prefix gist:  <https://w3id.org/semanticarts/ns/ontology/gist/>
prefix gistd: <https://w3id.org/semanticarts/ns/data/gist/>
prefix gistx: <https://w3id.org/semanticarts/ns/data/gist/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select ?aspect ?unitGroup ?unit

where {

      ?unitGroup a gist:UnitGroup ;
             gist:exponentOfAmpere    "0"^^xsd:decimal ; 
             gist:exponentOfBit       "0"^^xsd:decimal ; 
             gist:exponentOfCandela   "0"^^xsd:decimal ; 
             gist:exponentOfKelvin    "0"^^xsd:decimal ;  
             gist:exponentOfKilogram  "1"^^xsd:decimal ; 
             gist:exponentOfMeter     "0"^^xsd:decimal ; 
             gist:exponentOfMole      "0"^^xsd:decimal ; 
             gist:exponentOfNumber    "0"^^xsd:decimal ; 
             gist:exponentOfOther     "0"^^xsd:decimal ; 
             gist:exponentOfRadian    "0"^^xsd:decimal ; 
             gist:exponentOfSecond    "0"^^xsd:decimal ;
             gist:exponentOfSteradian "0"^^xsd:decimal ;
             gist:exponentOfUSDollar  "0"^^xsd:decimal ;
             .

      ?aspect gist:hasUnitGroup ?unitGroup . 

      ?unit   gist:isMemberOf ?unitGroup .

      }
order by ?aspect ?unitGroup ?unit
