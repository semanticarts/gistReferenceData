# validate basic relationships for Units of Measure

prefix gist:  <https://w3id.org/semanticarts/ns/ontology/gist/>
prefix gistd: <https://w3id.org/semanticarts/ns/data/gist/>
prefix gistx: <https://w3id.org/semanticarts/ns/data/gist/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select ?error ?unit ?group ?aspect ?discipline

where {

  {
   bind("Discipline with no Aspects" as ?error)
   filter not exists { ?aspect rdf:type gist:Aspect ; gist:isCategorizedBy ?discipline . }
  }
  union
  {
   bind("Aspect with no UnitGroup" as  ?error)
   ?aspect rdf:type gist:Aspect .
   filter not exists {?aspect gist:hasBroader*/gist:hasUnitGroup ?group . }
  }
  union
  {
   bind("UnitGroup with no Aspect" as ?error)
   ?group rdf:type gist:UnitGroup .
   filter not exists {?aspect gist:hasUnitGroup ?group . }
  }
  union
  {
   bind("UnitGroup with no Units" as ?error)
   ?group rdf:type gist:UnitGroup .
   filter not exists {?group ^gist:isMemberOf ?unit . }
  }
  union
  {
   bind("Unit with no UnitGroup" as ?error)
   ?unit rdf:type gist:UnitOfMeasure .
   filter not exists {?group ^gist:isMemberOf ?unit . }
  }
  union
  {
   bind("UnitGroup with wrong rdf:type" as ?error)
   ?aspect rdf:type gist:Aspect ;
           gist:hasUnitGroup ?group.
   filter not exists {?group rdf:type gist:UnitGroup . }
  }
  union
  {
   bind("Unit with wrong rdf:type" as ?error)
   ?group rdf:type gist:UnitGroup ;
           ^gist:isMemberOf ?unit.
   filter not exists {?unit rdf:type gist:UnitOfMeasure . }
  }
} 
order by ?error ?unit ?group ?aspect
