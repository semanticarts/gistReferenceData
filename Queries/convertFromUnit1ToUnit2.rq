# convert a numeric value from one unit to another, if the units measure the same aspect
# edit the input section for the from and to values

prefix gist:  <https://w3id.org/semanticarts/ns/ontology/gist/>
prefix gistd: <https://w3id.org/semanticarts/ns/data/gist/>
prefix gistx: <https://w3id.org/semanticarts/ns/data/gist/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select ?aspect ?fromUnit ?fromNumericValue ?toUnit ?toNumericValue

where {
  
# input 

  bind("27"^^xsd:decimal as ?fromNumericValue)
  bind(gistd:_UnitOfMeasure_foot as ?fromUnit)
  bind(gistd:_UnitOfMeasure_yard as ?toUnit)

# a result is returned only if fromUnit and toUnit both measure the same aspect (in the same unit group)
  
  ?fromUnit gist:isMemberOf ?unitGroup .  # both units are in the same unit group
  ?toUnit   gist:isMemberOf ?unitGroup .

  ?aspect gist:hasUnitGroup ?unitGroup .
 
  ?unitGroup rdf:type gist:UnitGroup .

  ?fromUnit gist:conversionFactor ?fromConversionFactor .
  
  ?toUnit   gist:conversionFactor ?toConversionFactor .
  
# multiply by the "from" conversion factor and divide by the "to" conversion factor  
  
  bind((?fromNumericValue * ?fromConversionFactor / ?toConversionFactor) as ?toNumericValue)
  }

